plugins {
    id 'java'
    id 'application'
    id 'org.springframework.boot' version '3.1.3'
    id 'io.spring.dependency-management' version '1.1.3'
    id 'org.graalvm.buildtools.native' version '0.9.24'
}

group = 'ru.ramlabs'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        url = uri("https://maven.pkg.github.com/ram042/json")
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
            password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
        }
    }
}

application {
    mainClass = 'ru.ramlabs.alldicesbot.App'
    applicationDefaultJvmArgs = ['-Dlogback.statusListenerClass=ch.qos.logback.core.status.NopStatusListener']
}

dependencies {
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    implementation 'com.google.guava:guava:32.1.2-jre'
    implementation 'com.github.ram042:json:6.0'
    implementation 'com.github.pengrad:java-telegram-bot-api:6.8.0'

    implementation 'org.springframework.boot:spring-boot-starter-web'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    implementation 'org.slf4j:slf4j-api:2.0.7'
    implementation 'ch.qos.logback:logback-classic:1.4.8'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.0'
    testImplementation 'org.assertj:assertj-core:3.24.2'
}

graalvmNative {
    agent {
        metadataCopy {
            mergeWithExisting = true
        }
    }
    binaries {
        main {
            quickBuild = true
            buildArgs.add("--no-fallback")
            buildArgs.add("--install-exit-handlers")
        }
    }
}

test {
    useJUnit()
}
